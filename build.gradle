plugins {
	id 'groovy'
	id 'maven-publish'
	id 'signing'
//	id 'eclipse'
	id "com.jfrog.bintray" version "1.8.5"
}

repositories {
	jcenter()
}

group = 'org.standardout'
version = '1.9.0-SNAPSHOT'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

jar {
	// include license into jar
	into 'META-INF', {
		from 'LICENSE'
	}
}

dependencies {
	implementation gradleApi()
	implementation localGroovy()
	implementation 'biz.aQute.bnd:biz.aQute.bndlib:5.0.1'
	implementation 'org.osgi:osgi.core:6.0.0'
	implementation 'commons-io:commons-io:2.6'
	implementation 'de.undercouch:gradle-download-task:4.0.4'
}

// package groovydoc into a jar file
task packageJavadoc(type: Jar, dependsOn: 'groovydoc') {
	from groovydoc.destinationDir
	archiveClassifier.set('javadoc')
}

// package source into a jar file
task packageSources(type: Jar) {
	from sourceSets.main.allSource
	archiveClassifier.set('sources')
}

// bintray
bintray { // task bintrayUpload
	user = project.getProperty('bintrayUser')
	key = project.getProperty('bintrayApiKey')

	publications = ['mavenJava']

	dryRun = false
	publish = !project.version.endsWith('-SNAPSHOT')
	pkg {
		def githubUrl = 'https://github.com/stempler/bnd-platform'
		repo = 'gradle-plugins'
		name = 'bnd-platform'
		desc = 'Build OSGi bundles and p2 repositories / Eclipse Update Sites from existing libraries and their dependencies, e.g. from Maven repositories. Useful for instance for creating a target platform for Eclipse/Equinox or Maven Tycho build from third party dependencies.'
		websiteUrl = githubUrl
		issueTrackerUrl = "$githubUrl/issues"
		vcsUrl = "${githubUrl}.git"
		licenses = ['Apache-2.0']
		labels = ['gradle', 'bnd', 'osgi', 'p2', 'eclipse', 'tycho']
		publicDownloadNumbers = true
		// version descriptor
		version {
			name = project.version
			attributes = ['gradle-plugin': "org.standardout.bnd-platform:${project.group}:bnd-platform"]
		}
	}

}

publishing {
    publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact packageJavadoc
			artifact packageSources
			pom {
				name = 'bnd-platform'
				description = 'Build OSGi bundles and Eclipse Update Sites from existing JARs, e.g. from Maven repositories (Plugin for Gradle)'
				url = 'https://github.com/stempler/bnd-platform'

				scm {
					url = 'scm:git:https://github.com/stempler/bnd-platform.git'
					connection = 'scm:git:https://github.com/stempler/bnd-platform.git'
					developerConnection = 'scm:git:https://github.com/stempler/bnd-platform.git'
				}

				licenses {
					license {
						name = 'The Apache Software License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution = 'repo'
					}
				}

				developers {
					developer {
						id = 'stempler'
						name ='Simon Templer'
						email = 'simon@templer.cc'
					}
				}
			}
        }
    }

	repositories {
		maven {
			name = 'Github'
			url = "https://maven.pkg.github.com/$githubUsername/bnd-platform"
			credentials {
				username = githubUsername
				password = githubToken ?: System.getenv("GITHUB_TOKEN")
			}
		}
		maven {
			name = 'MavenCentral'
			def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
			def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
			url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
			credentials {
				username = "$sonatypeUsername"
				password = "$sonatypePassword"
			}
		}
	}
}

afterEvaluate {
	tasks.withType(PublishToMavenRepository) {
		onlyIf {
			(repository != publishing.repositories.Github) || (githubUsername)
		}
	}
}

signing {
	required {
		gradle.taskGraph.hasTask(':publishMavenJavaPublicationToMavenCentralRepository')
	}
	sign publishing.publications.mavenJava
}


